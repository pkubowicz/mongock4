plugins {
    id 'org.springframework.boot' version '2.2.9.RELEASE'
    id 'com.github.ben-manes.versions' version '0.29.0'
}

allprojects {
    apply plugin: 'java-library'

    repositories {
        jcenter()
    }

    dependencies {
        implementation platform('org.springframework.boot:spring-boot-dependencies:2.2.9.RELEASE')
    }
}

subprojects {
    dependencies {
        implementation("org.springframework.boot:spring-boot-starter-data-mongodb-reactive")
    }
}

Object profileProperty = project.findProperty('profile')
Profile profile = profileProperty == null ? Profile.mongobee : Profile.valueOf(profileProperty as String)

dependencies {
    if (profile != Profile.mongock4) {
        runtimeOnly project(':mongock3')
    }
    if (profile != Profile.mongock3) {
        runtimeOnly project(':mongock4')
    }
    implementation('com.github.mongobee:mongobee:0.13')

    implementation("org.springframework.boot:spring-boot-starter-web")
}

bootRun {
    systemProperty('spring.profiles.active', profile)
}

enum Profile {
    mongock3, mongock4, mongobee
}
